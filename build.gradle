/*
 * abfall - convert ICS format trash calendar into a single Excel sheet
 * Copyright (C) 2011-2024 Thomas Jensen
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public
 * License, version 3, as published by the Free Software Foundation.
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied
 * warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
 * You should have received a copy of the GNU General Public License along with this program. If not, see
 * <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'application'
    id 'idea'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.sf.biweekly:biweekly:0.6.8'
    implementation 'org.apache.poi:poi:4.1.2'
    implementation 'org.apache.poi:poi-ooxml:4.1.2'
    implementation 'org.apache.poi:ooxml-schemas:1.4'
    implementation 'commons-cli:commons-cli:1.6.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.22.1'
    implementation 'org.apache.logging.log4j:log4j-iostreams:2.22.1'
}

application {
    mainClass = 'com.thomasjensen.abfall.Main'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation' << '-proc:none'
}

idea.module {
    downloadSources = true;
    downloadJavadoc = true;
    excludeDirs += file('.idea')
    excludeDirs += file('_support')
}
idea.project {
    jdkName = JavaVersion.VERSION_17
    languageLevel = JavaVersion.VERSION_17
}

jar {
    into('META-INF/') {
        from 'LICENSE.md'
    }
}
distributions {
    main {
        contents {
            from('LICENSE.md')
        }
    }
}
tasks.named('installDist').configure {
    destinationDir = layout.buildDirectory.dir('distributions/unpacked').get().getAsFile()
}
tasks.named('build').configure { it.dependsOn tasks.installDist }
